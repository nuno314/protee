def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader('UTF-8') { reader ->
        localProperties.load(reader)
    }
}

def flutterRoot = localProperties.getProperty('flutter.sdk')
if (flutterRoot == null) {
    throw new GradleException("Flutter SDK not found. Define location with flutter.sdk in the local.properties file.")
}

def flutterVersionCode = localProperties.getProperty('flutter.versionCode')
if (flutterVersionCode == null) {
    flutterVersionCode = '1'
}

def flutterVersionName = localProperties.getProperty('flutter.versionName')
if (flutterVersionName == null) {
    flutterVersionName = '1.0'
}

def keysProperties = new Properties()
def keysPropertiesFile = rootProject.file("keys.properties")
if (keysPropertiesFile.exists()) {
    keysProperties.load(new FileInputStream(keysPropertiesFile))
}

def googleMapAPIKey = keysProperties.getProperty('GOOGLE_MAP_API_KEY')
if (googleMapAPIKey == null) {
    googleMapAPIKey = ''
}

apply plugin: 'com.android.application'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'kotlin-android'
apply from: "$flutterRoot/packages/flutter_tools/gradle/flutter.gradle"
apply from: 'signing.gradle'

android {
    ndkVersion "25.1.8937393"
    compileSdkVersion 33

    compileOptions { 
        sourceCompatibility JavaVersion.VERSION_11         
        targetCompatibility JavaVersion.VERSION_11     
    }     
    
    kotlinOptions { 
        jvmTarget = '11'
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    lintOptions {
        disable 'InvalidPackage'
        checkReleaseBuilds false
    }

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 32
        versionCode flutterVersionCode.toInteger()
        versionName flutterVersionName
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.debug
        }
    }

    flavorDimensions "app"

    productFlavors {
        dev {
            applicationId "com.proteeDev"
            resValue "string", "app_name", "Protee Dev"
            resValue "string", "api_key", googleMapAPIKey
            signingConfig signingConfigs.dev
        }
        prod {
            applicationId "com.protee"
            resValue "string", "app_name", "Protee"
            resValue "string", "api_key", googleMapAPIKey
            signingConfig signingConfigs.prod
        }
    }
}

flutter {
    source '../..'
}

dependencies {
    implementation platform('com.google.firebase:firebase-bom:31.4.0')
    implementation 'com.google.firebase:firebase-analytics'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'com.facebook.android:facebook-android-sdk:[5,6)'
}
